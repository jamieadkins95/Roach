apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'io.fabric'

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.jamieadkins.gwent"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 47
        versionName "v2.0.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "CARD_DATA_VERSION", '"3"'
        }

        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
            buildConfigField "String", "CARD_DATA_VERSION", '"debug"'
        }
    }

    flavorDimensions "injection"

    productFlavors {
        prod {
            dimension "injection"
        }
        mock {
            dimension "injection"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true)
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    dexOptions {
        jumboMode true
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Support libraries.
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"
    implementation "com.android.support:preference-v14:$support_library_version"
    implementation "com.android.support:palette-v7:$support_library_version"
    implementation "com.android.support:customtabs:$support_library_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"

    // UI.
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'

    // Utils.
    implementation project(':commonutils')
    implementation 'me.xdrop:fuzzywuzzy:1.1.8'

    // RxJava.
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.1.8'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Firebase
    implementation "com.google.firebase:firebase-storage:$google_services_version"
    implementation "com.google.firebase:firebase-messaging:$google_services_version"
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC3'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.nytimes.android:cache3:3.0.0-beta'
    implementation 'com.nytimes.android:store3:3.0.0-beta'
    implementation 'com.nytimes.android:middleware3:3.0.0-beta'
    implementation 'com.nytimes.android:filesystem3:3.0.0-beta'

    implementation 'com.android.support:multidex:1.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation project(':database')
    implementation project(':view')
    implementation project(':core')
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://maven.google.com' }
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
