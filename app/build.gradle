apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

androidExtensions {
    experimental = true
}

kapt {
    correctErrorTypes = true
}

android {
    compileSdkVersion Versions.compile_sdk
    defaultConfig {
        applicationId "com.jamieadkins.gwent"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode Versions.app_version_code
        versionName Versions.app_version_name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "CARD_DATA_VERSION", '"3"'
        }

        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
            buildConfigField "String", "CARD_DATA_VERSION", '"debug"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "injection"

    productFlavors {
        prod {
            dimension "injection"
        }
        mock {
            dimension "injection"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true)
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    dexOptions {
        jumboMode true
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Support libraries.
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"
    implementation "com.android.support:preference-v14:$support_library_version"
    implementation "com.android.support:palette-v7:$support_library_version"
    implementation "com.android.support:customtabs:$support_library_version"
    implementation "com.android.support:animated-vector-drawable:$support_library_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"

    // UI.
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    implementation 'com.airbnb.android:epoxy:2.10.0'
    kapt 'com.airbnb.android:epoxy-processor:2.10.0'

    // Utils.
    implementation project(':commonutils')

    // RxJava.
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.1.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    // Firebase
    implementation 'com.jakewharton.timber:timber:4.7.0'

    implementation 'com.android.support:multidex:1.0.3'
    implementation Libs.kotlin

    implementation project(':database')
    implementation project(':data')
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://maven.google.com' }
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
