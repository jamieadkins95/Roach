apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

androidExtensions {
    experimental = true
}

kapt {
    correctErrorTypes = true
}

android {
    compileSdkVersion Versions.compile_sdk
    defaultConfig {
        applicationId "com.jamieadkins.gwent"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode Versions.app_version_code
        versionName Versions.app_version_name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "injection"

    productFlavors {
        prod {
            dimension "injection"
        }
        mock {
            dimension "injection"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true)
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    dexOptions {
        jumboMode true
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Support libraries.
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'

    // UI.
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation Libs.groupie
    implementation Libs.groupie_extensions

    // RxJava.
    implementation Libs.rxjava
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    // Firebase
    implementation Libs.timber

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation Libs.kotlin

    implementation project(':database')
    implementation project(':data')
    implementation project(':base')

    implementation Libs.dagger
    implementation Libs.dagger_support
    kapt Libs.dagger_compiler
    kapt Libs.dagger_processor

    implementation Libs.firebase
    implementation "com.crashlytics.sdk.android:crashlytics:${Versions.firebase_crashlytics}"
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://maven.google.com' }
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
