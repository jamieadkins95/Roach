apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

apply plugin: 'io.fabric'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.jamieadkins.gwent"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 47
        versionName "v2.0.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "CARD_DATA_VERSION", '"3"'
        }

        debug {
            debuggable true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
            buildConfigField "String", "CARD_DATA_VERSION", '"debug"'
        }
    }
    productFlavors {
        prod {

        }
        mock {

        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true)
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    dexOptions {
        jumboMode true
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Support libraries.
    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:design:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "com.android.support:cardview-v7:$support_library_version"
    compile "com.android.support:preference-v14:$support_library_version"
    compile "com.android.support:palette-v7:$support_library_version"

    // UI.
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }
    compile "com.android.support:customtabs:$support_library_version"

    // Utils.
    compile project(':commonutils')
    compile 'me.xdrop:fuzzywuzzy:1.1.8'

    // RxJava.
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.1.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.1.0'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'

    // Firebase
    compile "com.google.firebase:firebase-auth:$google_services_version"
    compile "com.firebaseui:firebase-ui-auth:1.2.0"
    compile "com.google.android.gms:play-services-auth:$google_services_version"
    compile "com.google.firebase:firebase-database:$google_services_version"
    compile "com.google.firebase:firebase-storage:$google_services_version"
    compile "com.google.firebase:firebase-messaging:$google_services_version"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.nytimes.android:cache3:3.0.0-beta'
    compile 'com.nytimes.android:store3:3.0.0-beta'
    compile 'com.nytimes.android:middleware3:3.0.0-beta'
    compile 'com.nytimes.android:filesystem3:3.0.0-beta'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://maven.google.com' }
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
