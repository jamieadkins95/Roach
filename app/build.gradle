apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.jamieadkins.gwent"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 37
        versionName "v1.5.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    dexOptions {
        jumboMode true
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Support libraries.
    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:design:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "com.android.support:cardview-v7:$support_library_version"
    compile "com.android.support:preference-v14:$support_library_version"
    compile "com.android.support:palette-v7:$support_library_version"

    // UI.
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }
    compile "com.android.support:customtabs:$support_library_version"

    // Common Utils.
    compile project(':commonutils')

    // Gwent API Wrapper.
    compile project(':jgaw')

    // RxJava.
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.0.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.0'

    // Firebase
    compile "com.google.firebase:firebase-auth:$google_services_version"
    compile "com.firebaseui:firebase-ui-auth:1.0.1"
    compile "com.google.android.gms:play-services-auth:$google_services_version"
    compile "com.google.firebase:firebase-database:$google_services_version"
    compile "com.google.firebase:firebase-storage:$google_services_version"
    compile "com.firebaseui:firebase-ui-storage:1.0.1"
    compile "com.google.firebase:firebase-messaging:$google_services_version"
    releaseCompile "com.google.firebase:firebase-crash:$google_services_version"

    // Testing.
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:3.0.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://maven.google.com' }
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
